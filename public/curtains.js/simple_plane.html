<!DOCTYPE html>
<html lang="en">
<head>
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <title>WebGL Core One</title>
    <meta charset="utf-8">
    <meta http-equiv="Content-Script-Type" content="text/javascript">
    <meta http-equiv="Content-Style-Type" content="text/css">
    <meta name="viewport" content="width=device-width, initial-scale=1.0, minimum-scale=1.0, maximum-scale=1.0 user-scalable=no">
    <link rel="shortcut icon" type="image/x-icon" href="../favicon.ico" />
    <link rel="stylesheet" type="text/css" href="../css/common.css" />
    <link rel="stylesheet" type="text/css" href="./simple_plane.css" />
        
</head>

<body>
    <span class="btn-back-index"><a href="../index.html">BACK</a></span>
    <div id="page-wrap">


        <div id="canvas"></div>

        <div class="curtain" data-vs-id="simple-plane-vs" data-fs-id="simple-plane-fs">
            <img src="./textures/plane-texture-3.jpg" data-sampler="simplePlaneTexture" />
        </div>
    </div>

    <script id="simple-plane-vs" type="x-shader/x-vertex">
        #ifdef GL_ES
        precision mediump float;
        #endif

        // default mandatory variables
        attribute vec3 aVertexPosition;
        attribute vec2 aTextureCoord;

        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;

        // our texture matrix uniform
        uniform mat4 simplePlaneTextureMatrix;

        // custom variables
        varying vec3 vVertexPosition;
        varying vec2 vTextureCoord;

        uniform float uTime;
        uniform vec2 uResolution;
        uniform vec2 uMousePosition;
        uniform float uMouseMoveStrength;


        void main() {

            vec3 vertexPosition = aVertexPosition;

            // get the distance between our vertex and the mouse position
            float distanceFromMouse = distance(uMousePosition, vec2(vertexPosition.x, vertexPosition.y));

            // calculate our wave effect
            float waveSinusoid = cos(5.0 * (distanceFromMouse - (uTime / 75.0)));

            // attenuate the effect based on mouse distance
            float distanceStrength = (0.4 / (distanceFromMouse + 0.4));

            // calculate our distortion effect
            float distortionEffect = distanceStrength * waveSinusoid * uMouseMoveStrength;

            // apply it to our vertex position
            vertexPosition.z +=  distortionEffect / 15.0;
            vertexPosition.x +=  (distortionEffect / 15.0 * (uResolution.x / uResolution.y) * (uMousePosition.x - vertexPosition.x));
            vertexPosition.y +=  distortionEffect / 15.0 * (uMousePosition.y - vertexPosition.y);

               gl_Position = uPMatrix * uMVMatrix * vec4(vertexPosition, 1.0);

               // varyings
               // here we use our texture matrix to calculate correct texture coords values
               vTextureCoord = (simplePlaneTextureMatrix * vec4(aTextureCoord, 0.0, 1.0)).xy;
               vVertexPosition = vertexPosition;
        }
    </script>
    <script id="simple-plane-fs" type="x-shader/x-fragment">

        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform float uTime;
        uniform vec2 uResolution;
        uniform vec2 uMousePosition;

        varying vec3 vVertexPosition;
        varying vec2 vTextureCoord;

        uniform sampler2D simplePlaneTexture;


        void main( void ) {

            // get our texture coords
            vec2 textureCoords = vec2(vTextureCoord.x, vTextureCoord.y);

            // apply our texture
            vec4 finalColor = texture2D(simplePlaneTexture, textureCoords);

            // fake shadows based on vertex position along Z axis
            finalColor.rgb -= clamp(-vVertexPosition.z, 0.0, 1.0);
            // fake lights based on vertex position along Z axis
            finalColor.rgb += clamp(vVertexPosition.z, 0.0, 1.0);

            // handling premultiplied alpha (useful if we were using a png with transparency)
            finalColor = vec4(finalColor.rgb * finalColor.a, finalColor.a);

            gl_FragColor = finalColor;
        }
    </script>

    <script src="../js/vendor/curtains/curtains.js" type="text/javascript"></script>
    <script src="./simple.plane.setup.js" type="text/javascript"></script>

</body>
  
</html>